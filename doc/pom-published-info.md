# org.starchartlabs.flare.pom-published-info

The `pom-published-info` plug-in is a convention plug-in which is intended to apply a standard behavior of adding information configured in `publishedInfo` (from the [published-info-base plug-in](published-info-base.md)) to POMs generated by MavenPublications

It is assumed that all projects are using Gradle's `maven-publish` plug-in.

## Application

Apply the plug-in via the buildscript classpath:

```
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.starchartlabs.flare', name: 'flare-publishing-plugins', version: '0.3.0'
    }
}

apply plugin: 'org.starchartlabs.flare.pom-published-info'
```

## Replaced Boilerplate

The plug-in has the effect of replacing the boilerplate:

```
apply plugin: 'org.starchartlabs.flare.source-jars'

project.publishing{
    publications.withType(MavenPublication.class).all{ pub ->
      artifact sourcesJar {
          classifier 'sources'
      }
      artifact javadocJar {
          classifier 'javadoc'
      }
    }
}

publications.withType(MavenPublication.class).all{ pub ->
  pom.withXml {
      asNode().appendNode('url', "${project.extensions.publishedInfo.url}")

      Node scmNode = asNode().appendNode('scm')
      scmNode.appendNode('url', "${project.extensions.publishedInfo.scm.url}")
      scmNode.appendNode('connection', "${project.extensions.publishedInfo.scm.connection}")
      scmNode.appendNode('developerConnection', "${project.extensions.publishedInfo.scm.developerConnection}")

      Node licensesNode = asNode().appendNode('licenses')

      project.extensions.publishedInfo.licenses.each{ lic ->
        Node licenseNode = licensesNode.appendNode('license')

        licenseNode.appendNode('name', "${lic.name}")
        licenseNode.appendNode('url', "${lic.url}")
        licenseNode.appendNode('distribution', "${lic.distribution}")
      }

      Node developersNode = asNode().appendNode('developers')

      project.extensions.publishedInfo.developers.each{ dev ->
        Node developerNode = developersNode.appendNode('developer')

        developerNode.appendNode('id', "${dev.id}")
        developerNode.appendNode('name', "${dev.name}")
        developerNode.appendNode('url', "${dev.url}")
      }
  }
}
```
